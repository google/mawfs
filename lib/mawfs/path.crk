# Copyright 2016 Google Inc. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

## Filesystem Path adapter.
module;

import crack.runtime ENOENT, ENOTDIR;
import crack.fs Path, PathIter, PathStat;
import crack.io FStr, SeekableReader, SeekableWriter, SEEK_CUR, SEEK_END,
    SEEK_SET;
import crack.lang Buffer, Exception, InvalidArgumentError, InvalidStateError,
    WriteBuffer, SystemError;
import crack.logger debug;
import crack.runtime S_IFDIR, S_IFREG;
import mawfs.blockstore Node, MODE_DIR, MODE_EXE;
import mawfs.cache CachedNode;

@import crack.ann implements;

## RWPath extends the interface of crack.fs.Path to random-access read and
## write operations.
@abstract class RWPath : Path {

    ## Write the data at the given position in the file.  If 'pos' is out of
    ## range, or the node is not a file, the behavior is undefined.
    @abstract void write(uint pos, Buffer data);

    ## Try to read 'data.cap' bytes from the file at pos.  If 'pos' is out of
    ## range, this should return an empty buffer.  If the node is not a file,
    ## the behavior is undefined.
    @abstract void read(uint pos, WriteBuffer data);
}

class _Seekable {
    intz _pos;
    RWPath _path;

    oper init(RWPath path) : _path = path {}

    @final intz _seek(intz offset, int whence) {
        if (whence == SEEK_CUR)
            _pos = offset;
        else if (whence == SEEK_END)
            _pos = _path.getSize() + offset;
        else if (whence == SEEK_SET)
            _pos = offset;
        else
            throw InvalidArgumentError();

        return _pos;
    }
}

class RWPathAppender : _Seekable @implements SeekableWriter {

    void flush() {}

    oper init(RWPath path) : _Seekable(path) {}

    void write(Buffer data) {
        _path.write(_pos, data);
        _pos += data.size;
    }

    void write(Buffer data) {
        _path.write(_pos, data);
    }

    intz seek(intz offset, int whence) {
        return _seek(offset, whence);
    }
}

class RWPathReader : _Seekable @implements SeekableReader {

    oper init(RWPath path) : _Seekable(path) {}

    intz seek(intz offset, int whence) {
        return _seek(offset, whence);
    }

    uint read(WriteBuffer buffer) {
        _path.read(_pos, buffer);
        _pos += buffer.size;
        return buffer.size;
    }
}

Path _makeMAWFSPath(CachedNode node, Path parent, String name);

class MAWFSIter : PathIter {
    Path __path;
    CachedNode __node;
    int i;

    oper init(Path path, CachedNode node) :
        __path = path,
        __node = node {
    }

    bool next() {
        if (i < __node.getChildCount()) {
            ++i;
            return true;
        } else {
            return false;
        }
    }

    Path elem() {
        return _makeMAWFSPath(__node.getChild(i), __path,
                              __node.getChildName(i)
                              );
    }

    bool isTrue() {
        return i < __node.getChildCount();
    }
}

class MAWFSPath : RWPath {

    ## The underlying cached node.  This may be null if the file does not
    ## exist.
    CachedNode __rep;

    MAWFSPath __parent;
    String __name;

    oper init(CachedNode rep) : __rep = rep, __name = '' {}

    oper init(CachedNode rep, MAWFSPath parent, String name) :
        __rep = rep,
        __parent = parent,
        __name = name {
    }

    SeekableWriter appender() {
        return RWPathAppender(this);
    }

    void __checkExists() {
        if (__rep is null)
            throw SystemError('File does not exist', ENOENT);
    }

    PathIter children() {
        __checkExists();
        if (__rep.getMode() & MODE_DIR)
            return MAWFSIter(this, __rep);
        else
            throw SystemError('Node is not a directory', ENOTDIR);
    }

    void delete() {
        __parent.__rep.deleteChild(__name);
    }

    void deleteTree() {}

    bool exists() {
        return __rep;
    }

    int getAccessTime() { return 0 }

    int getCreateTime() { return 0 }

    String getFullName() {
        return FStr() `$(__parent ? __parent.getFullName() : '')/$__name`;
    }

    int getModTime() { return 0 }

    int getMode() { return 0 }

    String getName() { return __name }

    uint64 getSize() {
        __checkExists();
        return __rep.getSize();
    }

    PathStat getStat() {
        __checkExists();
        PathStat stat = {};

        # The only attributes for the underlying filesystem are "directory"
        # and "executable".  Everything else is a normal file.
        # TODO: Make permissions configurable at the cache level.
        if (__rep.isDir())
            stat.st_mode = 0755 | S_IFDIR;
        else if (__rep.getMode() & MODE_EXE)
            stat.st_mode = 0755 | S_IFREG;
        else
            stat.st_mode = 0644 | S_IFREG;

        # TODO: change this to the user/group that started the server.
        stat.st_uid = 0;
        stat.st_gid = 0;
        stat.st_nlink = 1;
        return stat;
    }

    bool isDir() {
        if (!__rep)
            return false;
        return __rep.isDir();
    }

    bool isExecutable() {
        if (!__rep)
            return false;
        return __rep.getMode() & MODE_EXE;
    }

    bool isLink() { return false }

    SeekableWriter appender() { return null }

    void makeDir(bool errorOnExists) {
        node := Node();
        node.mode = MODE_DIR;
        __rep = __parent.__rep.addChild(__name, node);
    }

    void makeDirs() {}

    SeekableReader reader() {
        __checkExists();
        return RWPathReader(this);
    }

    SeekableWriter writer() {
        # Create a new node.
        if (__parent) {
            parentRep := __parent.__rep;
            __rep = parentRep.addChild(__name, Node());
        } else {
            # The root path should have been created with a cached node.
            throw Exception('Root has no cached node!');
        }
        return RWPathAppender(this);
    }

    Path parent() { return __parent }

    void truncate(uint64 newSize) {
        __checkExists();
        __rep.resize(newSize);
    }

    void moveTo(Path other) {
        o := MAWFSPath.cast(other);
        newParent := o.__parent;
        __parent.__rep.deleteChild(__name);
        newParent.__rep.addChild(other.getName(), __rep.node);
        o.__rep = __rep;
        __rep = null;
    }

    Path oper [](String child) {
        __checkExists();
        childNode := __rep.getChild(child);
        return MAWFSPath(childNode, this, child);
    }

    Path _createNode(String  path) { return null }

    Path oper /(String other) {
        return this[other];
    }

    void setMode(int mode) {}

    Path moveToDir(Path other) { return null }

    void write(uint pos, Buffer data) {
        __checkExists();
        if (__rep.isDir())
            throw InvalidStateError(
                FStr() `File $(getFullName()) is a directory.`);

        __rep.write(pos, data);
    }

    void read(uint pos, WriteBuffer data) {
        __checkExists();
        __rep.read(pos, data);
    }
}

Path _makeMAWFSPath(CachedNode node, Path parent, String name) {
    return MAWFSPath(node, MAWFSPath.cast(parent), name);
}
