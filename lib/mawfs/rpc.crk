## MAWFS RPC client.

import crack.ascii parseInt;
import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.functor Functor3;
import crack.io cerr, Formatter, FStr, StringWriter, Reader;
import crack.lang cmp, makeHashVal, AppendBuffer, Exception, WriteBuffer;
import crack.logger.ctx Log;
import crack.net resolve, Address, InetAddress;
import crack.net.comm2 Comm, CommHandler, CommHandlerFactory, NewConnection,
    SyncCommProxy, UserHandler, WaiterFor;
import crack.protobuf readMessageFromString, Field, Message, ProtoWriter;
import crack.regex Regex;
import crack.serial SerialReader, SerialWriter;
import mawfs.rawchunk GeneralCallback, JournalBlock, RawChunkReader;

@import crack.ann cvars, impl, interface, struct;
@import crack.protobuf.ann  protobuf;

const DEFAULT_PORT := 9119
const IPV4_ADDR := Regex(r'(\d+)\.(\d+)\.(\d+)\.(\d+)');

## Raised when there is an application error.
class AppError : Exception {
    oper init(String message) : Exception(message) {}
}

@protobuf {
    version = 1

    ## The general RPC message envelope.  Everything gets sent across the wire
    ## in a serialized string containing one of these.
    message RPCMessage {

        ## The message id.  When sent with a request, the response should have
        ## the same id.  Messages without a non-zero message id are presumed
        ## to be asynchronous and don't get a response.
        optional int32 id = 1;

        ## The message to be called.
        optional string method = 2;

        ## The request body.  Should only be present for a request. This will
        ## be another protobuf appropriate for the "request" part of the
        ## method.
        optional string request = 3;

        ## The response body. Should only be present for a response.  The
        ## response protobuf.
        optional string response = 4;

        ## If there was an error processing a message, this gets returned in
        ## the response.
        optional string error = 5;
    }

    message GetObjectRequest {
        ## Message digest.
        optional string digest = 1;
    }

    message GetObjectResponse {
        optional string data = 1;
    }

    message GetHeadRequest {
        optional string branch = 1;
    }

    message GetHeadResponse {
        optional string digest = 1;
    }

    message GetFileRequest {
        optional string filename = 1;
    }

    message GetFileResponse {
        optional string data = 1;
    }

    message GetJournalBlockRequest {
        optional bytes firstBlockDigest = 1;
        optional string branch = 2;
        optional int32 pos = 3;
    }

    message GetJournalBlockResponse {
        optional bytes firstBlockDigest = 1;
        optional bytes contents = 2;
        optional bool done = 3;
    }

    message PullBranchRequest {
        optional string branch = 1;
        optional string peer = 2;
    }

    message PullBranchResponse {
        optional string localName = 1;
    }

    message MergeRequest {
        optional string branch = 1;
    }

    message MergeResponse {
        ## If we were able to merge clean, commit is set to the digest of the
        ## new commit and mergeBranch is null.  Otherwise, mergeBranch is the
        ## name of the temporary merge branch.
        optional string mergeBranch = 1;
        optional string commit = 2;
        repeated string conflict = 3;
    }
}

class MergeResult : MergeResponse {
    oper init(String mergeBranch, String commit, Array[String] conflicts) {
        this.mergeBranch = mergeBranch;
        this.commit = commit;
        conflict = conflicts;
    }
}

@interface InstIFace {
    @abstract void pullBranch(String branch, String peer,
                              GeneralCallback[String] callback
                              );

    @abstract void merge(String
                         branch, GeneralCallback[MergeResponse] callback
                         );
}

InstIFace _inst;
void setInst(InstIFace inst) { _inst = inst }

alias RPCWaiter = WaiterFor[RPCMessage, int];

class _FakeReader @impl Reader {
    uint read(WriteBuffer buf) {
        return buf.size = 0;
    }
}

## Reads protobuf strings from an append buffer.
class _BufferSerialReader {
    AppendBuffer __buffer = {4096};
    SerialReader __reader = {_FakeReader()};

    oper init(AppendBuffer buffer) : __buffer = buffer {
        __reader.buf = __buffer;
    }

    ## Read a string, returns null if no complete string is in the buffer.
    @final String readString() {
        return __reader.readString(false);
    }
}

## Synchronous client handler.
class _Handler @impl UserHandler {
    _BufferSerialReader __reader;
    HashMap[int, RPCWaiter] __waiters = {};

    void process(CommHandler comm) {
        if (!__reader)
            __reader = _BufferSerialReader(comm.inBuf);

        data := __reader.readString();
        if (data) {
            RPCMessage response = {};
            readMessageFromString(response, data);

            waiter := __waiters.get(response.id);
            if (waiter)
                waiter.setResult(response);
            else
                comm.log.error `No response found for message id $(response.id)\n`;
        }
    }

    void terminate(CommHandler comm) {
        # Close all of the open connections.
        for (waiter :in __waiters) {
            RPCMessage response = {};
            response.id = int32(waiter.key);
            response.error = 'Connection closed prematurely.';
            waiter.val.setResult(response);
        }
    }

    void addResponder(Object waiter) {
        w := RPCWaiter.cast(waiter);
        __waiters[w.info] = w;
    }
}

## Thrown whenever we receive an error from the remote object.
class RemoteError : Exception {
    oper init(String message) : Exception(message) {}
}

## An InetAddress extended that retains its hostname (or IP address, if
## created that way).
class ExtendedAddress : InetAddress {
    String __hostname;
    oper init(String hostname, byte a, byte b, byte c, byte d, int port) :
        InetAddress(a, b, c, d, port),
        __hostname = hostname {
    }

    oper init(String hostname, uint32 addr, int port) :
        InetAddress(addr, port),
        __hostname = hostname {
    }

    String getHostname() { return __hostname }
}

## Parse an address of the form host:port.  "host" may be either a hostname or
## an ip address.
ExtendedAddress parseAddr(String addr) {
    int port;
    String host;
    if ((colon := addr.lfind(b':')) != -1) {
        host = addr.substr(0, colon);
        port = parseInt(addr.substr(colon + 1));
    } else {
        host = addr;
        port = DEFAULT_PORT;
    }

    # Now check for an ipv4 address.
    ExtendedAddress a;
    if (m := IPV4_ADDR.match(host)) {
        a = ExtendedAddress(host, byte(parseInt(m.group(1))),
                            byte(parseInt(m.group(2))),
                            byte(parseInt(m.group(3))),
                            byte(parseInt(m.group(4))),
                            port
                            );
    } else {
        addrs := resolve(host);
        a = ExtendedAddress(host, InetAddress.cast(addrs[0]).getAddr(), port);
    }
    return a;
}

class PeerProxy {
    Comm __comm;
    SyncCommProxy __proxy;
    Log __log;
    int32 __lastId;

    oper init(Comm comm, Log log, String addr) : __comm = comm, __log = log {
        a := parseAddr(addr);
        __log.info `connecting to $a`;
        __proxy = __comm.getSyncProxy(a, _Handler());
    }

    oper init(Comm comm, Log log, Address addr) : __comm = comm, __log = log {
        __log.info `connecting to $addr`;
        __proxy = __comm.getSyncProxy(addr, _Handler());
    }

    ## Test constructor.
    class TestOnly {}
    oper init(TestOnly test) {}

    String __serialize(RPCMessage message) {
        StringWriter out = {};
        SerialWriter sw = {out};
        sw.write(message.toString());
        return out.string();
    }

    RPCMessage __sendSync(RPCMessage message) {
        # Serialize and send the message.
        serialized := __serialize(message);
        waiter := RPCWaiter(message.id);
        __proxy.sendSync(serialized, waiter);
        return waiter.getResult();
    }

    ## Invoke a remote method.
    String __invoke(String method, String contents) {
        RPCMessage msg = {};
        msg.id = ++__lastId;
        msg.method = method;
        msg.request = contents;
        resp := __sendSync(msg);

        if (resp.error)
            throw RemoteError(resp.error);

        return resp.response;
    }

    ## Shutdown the proxy connection.
    void shutdown() {
        __proxy.close();
    }

    ## Returns the object with the specified digest, null if the peer doesn't
    ## have it.
    String getObject(String digest) {
        GetObjectRequest req = {};
        req.digest = digest;
        GetObjectResponse resp = {};
        respData := __invoke('getObject', req.toString());
        readMessageFromString(resp, respData);
        return resp.data;
    }

    String getHead(String branch) {
        GetHeadRequest req = {};
        req.branch = branch;
        GetHeadResponse resp = {};
        respData := __invoke('getHead', req.toString());
        readMessageFromString(resp, respData);
        return resp.digest;
    }

    ## Get a file from the remote backing store.
    ##
    ## This method can probably subsume getObject() and getHead().
    String getFile(String filename) {
        GetFileRequest req = {};
        req.filename = filename;
        GetFileResponse resp = {};
        respData := __invoke('getFile', req.toString());
        readMessageFromString(resp, respData);
        return resp.data;
    }

    ## Gets the next journal block from the remote peer.  See RawChunkReader
    ## for details.
    JournalBlock getJournalBlock(String firstBlockDigest,
                                 String branch,
                                 uint pos
                                 ) {
        GetJournalBlockRequest req = {};
        GetJournalBlockResponse resp = {};
        req.firstBlockDigest = firstBlockDigest;
        req.branch = branch;
        req.pos = int32(pos);
        respData := __invoke('getJournalBlock', req.toString());
        readMessageFromString(resp, respData);
        return JournalBlock(resp.firstBlockDigest, resp.contents, resp.done);
    }

    ## Pull the specified branch from the specified peer or from any peer in
    ## the network if 'peer' is null.
    ## Returns the local name of the branch.
    String pullBranch(String branch, String peer) {
        PullBranchRequest req = {};
        PullBranchResponse resp = {};
        req.branch = branch;
        req.peer = peer;
        respData := __invoke('pullBranch', req.toString());
        readMessageFromString(resp, respData);
        return resp.localName;
    }

    ## Initiates a merge of the active branch with the specified merge branch.
    MergeResponse merge(String branch) {
        MergeRequest req = {};
        MergeResponse resp = {};
        req.branch = branch;
        respData := __invoke('merge', req.toString());
        readMessageFromString(resp, respData);
        return MergeResult(resp.mergeBranch, resp.commit, resp.conflict);
    }
}

## Server-side RPC message context.
class Context {
    Log log;
    RawChunkReader reader;

    oper init(RawChunkReader reader) : reader = reader {}

    ## Utility function for writing a response message.
    void writeResponse(CommHandler comm, Message response, int32 requestId) {
        RPCMessage reply = {};
        reply.id = requestId;
        reply.response = response.toString();
        StringWriter backing = {};
        SerialWriter sw = {backing};
        sw.write(reply.toString());
        comm.write(backing.string());
        log.debug I`sent to poller thread: $(reply.toString().getRepr())`;
    }

    void writeError(CommHandler comm, String errorMessage, int32 requestId) {
        RPCMessage reply = {};
        reply.id = requestId;
        reply.error = errorMessage;
        StringWriter backing = {};
        SerialWriter sw = {backing};
        sw.write(reply.toString());
        comm.write(backing.string());
        log.debug I`sent to poller thread: $(reply.toString().getRepr())`;
    }
}

# Mapping from method name to method processing function.  In addition to a
# context, a method handler accepts a string containing the serialized request
# and an AppendBuffer into which to write the serialized response.
alias MethodHandler = Functor3[void, Context, RPCMessage, CommHandler];
alias MethodMap = HashMap[String, MethodHandler];

## Handles communication from an RPC client.
class ClientHandler @impl UserHandler {

    ## Serial reader for input.
    _BufferSerialReader reader;

    ## The session context.
    Context __context;

    MethodMap __methods;

    oper init(MethodMap methods, Context context) :
        __methods = methods,
        __context = context {
    }

    void process(CommHandler comm, RPCMessage msg) {
        comm.log.debug `processing method $(msg.method)`;
        String error;

        handler := __methods.get(msg.method);
        if (handler) {
            try {
                comm.log.debug `calling handler`;
                handler(__context, msg, comm);
                return;
            } catch (Exception ex) {
                error = FStr() `$ex`;
            }
        } else {
            comm.log.error `got unknown method $(msg.method)`;
            error = FStr() `Method $(msg.method.getRepr()) not found`;
        }

        # if we haven't returned yet, we have to deal with an error.

        # If there's no message id, the message is asynchronous and we don't
        # return a response.
        if (!msg.id)
            return;


        # Wrap the reply in a string and ship it back to the client.
        comm.log.debug I`sending reply, error $error`;
        __context.writeError(comm, error, msg.id);
    }

    void process(CommHandler comm) {
        # Initialize the reader if we haven't done so.
        if (!reader)
            reader = _BufferSerialReader(comm.inBuf);
        if (!__context.log)
            __context.log = comm.log;

        while (comm.inBuf) {
            # Get the next message, make sure we were able to read it.
            serializedMsg := reader.readString();
            if (!serializedMsg)
                # Assume this is an incomplete message.
                return;

            # We've got a complete message, so process it.
            RPCMessage msg = {};
            readMessageFromString(msg, serializedMsg);

            process(comm, msg);
        }
    }

    void terminate(CommHandler comm) {
        # TODO when the RPC client terminates, we want to close all active
        # tunnels for the client.
    }

    void addResponder(Object object) {}
}

# --- Server-side method implementation functions. ---

void getObject(Context context, RPCMessage msg, CommHandler comm) {
    GetObjectRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[String] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(String contents, Exception ex) {
            GetObjectResponse resp = {};
            resp.data = contents;
            context.writeResponse(comm, resp, requestId);
        }
    }

    context.reader.readRawChunk(req.digest,
                                ResponseCallback(context, comm, msg.id));
}

void getHead(Context context, RPCMessage msg, CommHandler comm) {
    GetHeadRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[String] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(String digest, Exception ex) {
            GetHeadResponse resp = {};
            resp.digest = digest;
            context.writeResponse(comm, resp, requestId);
        }
    }

    context.reader.getHead(req.branch, ResponseCallback(context, comm, msg.id));
}

void getFile(Context context, RPCMessage msg, CommHandler comm) {
    GetFileRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[String] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(String data, Exception ex) {
            GetFileResponse resp = {};
            resp.data = data;
            context.writeResponse(comm, resp, requestId);
        }
    }

    context.reader.getFile(req.filename,
                           ResponseCallback(context, comm, msg.id));
}

void getJournalBlock(Context context, RPCMessage msg, CommHandler comm) {
    GetJournalBlockRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[JournalBlock] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(JournalBlock block, Exception ex) {
            GetJournalBlockResponse resp = {};
            resp.firstBlockDigest = block.firstBlockDigest;
            resp.contents = block.contents;
            resp.done = block.done;
            context.writeResponse(comm, resp, requestId);
        }
    }

    context.reader.getJournalBlock(req.firstBlockDigest, req.branch, req.pos,
                                   ResponseCallback(context, comm, msg.id)
                                   );
}

void pullBranch(Context context, RPCMessage msg, CommHandler comm) {
    PullBranchRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[String] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(String localName, Exception ex) {
            # Pull-branch is handled asynchronously, so we have to check
            # the 'ex' argument.
            if (ex) {
                context.writeError(comm, ex.text, requestId);
            } else {
                PullBranchResponse resp = {};
                resp.localName = localName;
                context.writeResponse(comm, resp, requestId);
            }
        }
    }

    _inst.pullBranch(req.branch, req.peer,
                     ResponseCallback(context, comm, msg.id));
}

void merge(Context context, RPCMessage msg, CommHandler comm) {
    MergeRequest req = {};
    readMessageFromString(req, msg.request);

    class ResponseCallback @impl GeneralCallback[MergeResponse] {
        @cvars {Context context; CommHandler comm; int32 requestId;}
        void oper call(MergeResponse resp, Exception ex) {
            # merge is handled asynchronously, so we have to check
            # the 'ex' argument.
            if (ex) {
                context.writeError(comm, ex.text, requestId);
            } else {
                context.writeResponse(comm, resp, requestId);
            }
        }
    }

    _inst.merge(req.branch, ResponseCallback(context, comm, msg.id));
}

class PeerServiceCommFactory @impl CommHandlerFactory {
    MethodMap __methods = {};
    Context __context;

    oper init(Context context) : __context = context {
        __methods['getObject'] = MethodHandler.Wrap(getObject);
        __methods['getHead'] = MethodHandler.Wrap(getHead);
        __methods['getJournalBlock'] = MethodHandler.Wrap(getJournalBlock);
        __methods['getFile'] = MethodHandler.Wrap(getFile);
        __methods['pullBranch'] = MethodHandler.Wrap(pullBranch);
        __methods['merge'] = MethodHandler.Wrap(merge);
    }

    CommHandler makeCommHandler(NewConnection conn) {
        conn.log.info `accepted connection from $(conn.accepted.addr)\n`;
        log := Log(conn.log, FStr() `server-client-$(conn.accepted.addr)`);
        return CommHandler(log, ClientHandler(__methods, __context));
    }
}
